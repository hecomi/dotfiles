" quickfix のエラー箇所を波線でハイライト
execute "highlight qf_error_ucurl gui=undercurl guisp=Red"
let g:hier_highlight_group_qf  = "qf_error_ucurl"

" quickfix に出力して、ポッポアップはしない outputter/quickfix
" すでに quickfix ウィンドウが開いている場合は閉じるので注意
let s:silent_quickfix = quickrun#outputter#quickfix#new()
function! s:silent_quickfix.finish(session)
    call call(quickrun#outputter#quickfix#new().finish, [a:session], self)
    :cclose
    " vim-hier の更新
    :HierUpdate
    " quickfix への出力後に quickfixstatus を有効に
    :QuickfixStatusEnable
endfunction
" quickrun に登録
call quickrun#register_outputter("silent_quickfix", s:silent_quickfix)


" シンタックスチェック用の quickrun.vim のコンフィグ
" gcc 版
let g:quickrun_config["CppSyntaxCheck_gcc"] = {
    \ "type"  : "cpp",
    \ "exec"      : "%c %o %s:p ",
    \ "command"   : "g++",
    \ "cmdopt"    : "-fsyntax-only -std=gnu++0x ",
    \ "outputter" : "silent_quickfix",
    \ "runner"    : "vimproc"
\ }

" msvc 版
" .h ファイルの場合はうまく動かない
let g:quickrun_config["CppSyntaxCheck_msvc"] = {
    \ "type"  : "cpp",
    \ "exec"      : "%c %o %s:p ",
    \ "command"   : "cl.exe",
    \ "cmdopt"    : "/Zs ",
    \ "outputter" : "silent_quickfix",
    \ "runner"    : "vimproc",
    \ "output_encode" : "sjis"
\ }

" ファイルの保存後に quickrun.vim が実行するように設定する
autocmd BufWritePost *.cpp,*.h,*.hpp :QuickRun CppSyntaxCheck_gcc
